#!/usr/bin/env oh

## #### Symbols
##
## Oh's default data type is the symbol. Like other programming languages,
## a symbol in oh, can be one or more alphanumeric characters. Unlike other
## programming languages, there is no restriction that a symbol start with,
## or even contain, an alphabetic character. Oh also permits the following
## characters in symbols: `!`, `*`, `+`, `,`, `-`, `/`, `=`,`?`, `[`, `]`,
## and `_`.
##
## The command,
##
#{
write this-is-a-symbol
#}
##
## produces the output,
##
#+     this-is-a-symbol
##

write !: is-symbol !
write *: is-symbol *
write +: is-symbol +
write ,: is-symbol ,
write -: is-symbol -
write /: is-symbol /
write =: is-symbol =
write ?: is-symbol ?
write [: is-symbol [
write ]: is-symbol ]
# write _: is-symbol _ #TODO: Bring back when we've made $ prefix mandatory.

#-     ! true
#-     * true
#-     + true
#-     , true
#-     - true
#-     / true
#-     = true
#-     ? true
#-     [ true
#-     ] true
# #-     _ true

define common: import: ... common.oh

$common::apply-predicates this-is-a-symbol

#-     is-atom "x => true"
#-     is-boolean "x => false"
#-     is-builtin "x => false"
#-     is-channel "x => false"
#-     is-cons "x => false"
#-     is-continuation "x => false"
#-     is-float "x => false"
#-     is-integer "x => false"
#-     is-method "x => false"
#-     is-null "x => false"
#-     is-number "x => false"
#-     is-object "x => false"
#-     is-pipe "x => false"
#-     is-rational "x => false"
#-     is-status "x => false"
#-     is-string "x => false"
#-     is-symbol "x => true"
#-     is-syntax "x => false"

