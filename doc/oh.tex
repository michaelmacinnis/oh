\documentclass[12pt]{book}
\usepackage{listings, setspace, url}

\setlength{\parindent}{0pt}
\setlength{\parskip}{1ex}

\begin{document}

\def\blfootnote{\xdef\@thefnmark{}\@footnotetext}

\lstset{aboveskip={\bigskipamount}, basicstyle=\tt,
		language=sh, showspaces=false, showstringspaces=false
}

\title{\Huge \bf Using oh}
\author{}
\date{}

\maketitle


\chapter[Introducion]{Introduction\footnote{Much of this chapter
shamelessly copied from ``An Introduction to the UNIX Shell''.~\cite{sh}}}


Oh provides a command-line interface to Unix and Unix-like systems.


\section{Simple commands}

Simple commands consist of one or more words separated by blanks.
The first word is the name of the command to be executed; any remaining
words are passed as arguments to the command. For example,

\begin{lstlisting}
	ls -la
\end{lstlisting}

is a command that prints a list of files in the current directory.
The argument -l tells ls to print status information, size and the
creation date for each file.

Multiple commands may be written on the same line separated by a
semicolon.


\section{Input output redirection}

Standard input, standard output and standard error are initially
connected to the terminal. Standard output may be sent to a file.  

\begin{lstlisting}
	ls -l >file
\end{lstlisting}

The notation \verb%>%file is interpreted by the shell and is not
passed as an argument to ls. If the file does not exist then the
shell creates it, otherwise the original contents of the file are
replaced with the output from ls. Output may also be appended to
a file.

\begin{lstlisting}
	ls -l >>file
\end{lstlisting}

Standard error may be redirected

\begin{lstlisting}
	ls -l !>file
\end{lstlisting}

or appended to a file

\begin{lstlisting}
	ls -l !>>file
\end{lstlisting}

Standard input may also be redirected

\begin{lstlisting}
	wc -l <file
\end{lstlisting}


\section{Pipelines and filters}

The standard output of one command may be connected to the standard
input of another command using the pipe operator

\begin{lstlisting}
	ls | wc -l
\end{lstlisting}

The commands connected in this way constitute a pipeline. The
overall effect is the same as

\begin{lstlisting}
	ls >file; wc -l <file
\end{lstlisting}

except that no file is used. Instead the two processes are connected
by a pipe and are run in parallel.

A filter is a command that reads its standard input, transforms it
in some way, and sends the result to standard output. One such filter,
grep, selects from its input those lines that contain some specified
string. 

\begin{lstlisting}
		ls | grep old
\end{lstlisting}

A pipeline may consist of more than two commands.

\begin{lstlisting}
		ls | grep old | wc -l
\end{lstlisting}


\section{File name generation}

The oh shell provides a mechanism for generating a list of file
names that match a pattern.

\begin{lstlisting}
		ls -l *.c
\end{lstlisting}

generates, as arguments to ls, all file names in the current
directory that end in .c. The character * is a pattern that will
match any string including the empty string. In general 
patterns are specified as follows.

\begin{center}
\begin{tabular}{|c|l|}
\hline
\verb%*% & Matches any sequence of zero or more characters. \\
\verb%?% & Matches any single character. \\
\verb%[...]% & Matches any one of the characters enclosed. A pair separated \\
& by a minus will match a lexical range of characters. \\
\hline
\end{tabular}
\end{center}

For example,

\begin{lstlisting}
		[a-z]*
\end{lstlisting}

matches all names in the current directory beginning with one of
the letters a through z.

\begin{lstlisting}
		/usr/home/?
\end{lstlisting}

matches all names in the directory /usr/home that consist of a
single character.

There is one exception to the general rules given for patterns.
The character . at the start of a file name must be explicitly
matched.

\begin{lstlisting}
		echo *
\end{lstlisting}

will therefore echo all file names not beginning with a . in the
current directory.

\begin{lstlisting}
		echo .*
\end{lstlisting}

will echo all those file names that begin with . as the . was
explicitly specified. This avoids inadvertent matching of the names
. and .. which mean the current directory and the parent directory
respectively.


\section{Escaping and quoting} 

Characters that have a special meaning to the shell, such as \verb%<%
\verb%>% \verb%|% \verb%&%, are called metacharacters. Any character
preceded by a \\ is escaped and loses its special meaning, if any. The
\verb%\% is elided so that

\begin{lstlisting}
		echo \?
\end{lstlisting}

will echo a single \verb%?%, and

\begin{lstlisting}
		echo \\
\end{lstlisting}

will echo a single \\. To allow long commands to be continued over
more than one line newlines can be escaped with a \\.

\verb%\% is convenient for escaping single characters. When more
than one character needs escaping the above mechanism is clumsy and
error prone. A string of characters may be quoted by enclosing the
string between double quotes.

\begin{lstlisting}
		echo "xx****xx"
\end{lstlisting}

will echo

\begin{lstlisting}
		xx****xx
\end{lstlisting}

The quoted string may not contain an unescaped double quote but may
contain newlines, which are preserved.


\chapter[Oh programming]{Oh programming\footnote{Much of this
chapter shamelessly copied from ``Yet Another Scheme
Tutorial''.~\cite{YAST}}}

In addition to providing a command-line interface to Unix and
Unix-like systems, oh is also a programming language.


\section{Using oh as a calculator}

We can add the numbers 1 and 2, using oh.

\begin{lstlisting}
	add 1 2
\end{lstlisting}

To see the result we have to use the command write,

\begin{lstlisting}
	write (add 1 2)
\end{lstlisting}

or more convieniently,

\begin{lstlisting}
	write: add 1 2
\end{lstlisting}

We can perform other operations.

\begin{lstlisting}
	write: sub 10 3 5
	write: mul 2 3
	write: div 6 3
	write: mod 6 5
\end{lstlisting}

Floating point numbers can also be used

\begin{lstlisting}
	write: div 1.0 9.0
\end{lstlisting}

and operations can be nested.

\begin{lstlisting}
	write: mul (add 2 3) (sub 5 3)
\end{lstlisting}


\section{Making lists}

Lists, in oh, are composed of cons cells. A cons cell is a pair of
values. The first value is refered to as the car of the cons cell and
the second as the cdr. The command cons is used to create a new cons
cell.

\begin{lstlisting}
	write: cons 1 2
\end{lstlisting}

The car and cdr of a cons cell can also be cons cells. A list is formed
by chaining cons cells. The cdr of each cons cell is set to the next
element in the list. The cdr of the last element in the list is set to
empty list, which is written as ().

\begin{lstlisting}
	write: cons 1 (cons 2 (cons 3 ()))
\end{lstlisting}

or more convieniently,

\begin{lstlisting}
	write: cons 1: cons 2: cons 3 ()
\end{lstlisting}

or even better,

\begin{lstlisting}
	write: list 1 2 3
\end{lstlisting}

The commands car and cdr can be used to access the car and cdr of a
cons cell, respectively.

\begin{lstlisting}
	write: car: list 1 2 3
	write: cdr: list 1 2 3
\end{lstlisting}

Lists can also be represented directly but must be quoted, by preceding
them with a single quote, to stop oh from trying to evaluate them. The
empty list is an exception to this rule as it evaluates to itself.

\begin{lstlisting}
	write: car '(1 2 3)
	write: cdr '(1 2 3)
\end{lstlisting}


\section{Methods}

The define and method commands allow the creation of named methods.

\begin{lstlisting}
	define hello: method () as {
		write "Hello, World!"
	}
\end{lstlisting}

Once defined a method can be called in the same way as other commands.

\begin{lstlisting}
	hello
\end{lstlisting}

Methods can take arguments.

\begin{lstlisting}
	define sum3: method (a b c) as {
		add a b c
	}
	sum3 1 2 3
\end{lstlisting}


\section{Modules}

Methods can be saved in a file and then imported. If the function hello
was defined in the file hello.oh as shown below

\begin{lstlisting}
	public hello: method () as {
		write "Hello, World!"
	}
\end{lstlisting}

it could be imported and called.

\begin{lstlisting}
	define hello-module: import hello.oh
	hello-module::hello
\end{lstlisting}

Modules, in oh, are objects. When importing a module everything defined
using the command public is visible while everything defined using the
command define is private to that module.


\section{Objects}
An object can be created explicitly with the object command.

\begin{lstlisting}
	define point: method r s: object {
		define x: integer r
		define y: integer s

		public get-x: method self () as {
			return self::x
		}

		public get-y: method self () as {
			return self::y
		}

		public move: method self (a b) as {
			set self::x: add self::x a
			set self::y: add self::y b
		}
	}

	define p: point 0 0
\end{lstlisting}

In the above example, we create a generator method, point, which acts
very much like a constructor in class-based languages, but we could also
use the clone method to create a clone of an existing object or the
child method to create a descendent object.  The methods get-x, get-y
and move could also be moved up and out to the same level as the
definition for 'point'. All objects created by the generator method,
point, maintain an implicit parent link to their enclosing scope and,
in doing so, would automatically delegate calls to these methods to their
parent object. If, for example, we wanted other objects like circles
or squares, we could use methods defined in the enclosing scope to
implement shared behaviour.

Dynamic communication patterns are enabled by the fact that public slots
can be updated or added to any object, at runtime.

\begin{lstlisting}
	p::public show: method self () as {
		echo: add "(" self::x "," self::y ")\n"
	}
\end{lstlisting}


\section{Control flow}

Like other languages, oh has an if statement.

\begin{lstlisting}
	if (cd /tmp) {
		# Do stuff in /tmp.
	}
\end{lstlisting}

If statements may have an else clause.

\begin{lstlisting}
	if (cd /tmp) {
		# Do stuff in /tmp.
	} else {
		write "Couldn't cd to /tmp."
	}
\end{lstlisting}

If statements can be chained.

\begin{lstlisting}
	if (cd /tmp) {
		# Do stuff in /tmp.
	} else: if (cd /var/tmp) {
		# Do stuff in /var/tmp
	} else {
		write "Couldn't cd to /tmp or /var/tmp."
	}
\end{lstlisting}

Oh also has while loops.

\begin{lstlisting}
	define line: readline
	while line {
		write line
		set line: readline
	}
\end{lstlisting}

Oh's for statement takes a list and a method as arguments and returns the
list of return values from each invocation of the method on every element
in the list.

\begin{lstlisting}
	write: for '(1 2 3): method n {
	    return: mul n 2
	}
\end{lstlisting}


\section{Channels and concurrency}

Oh's channel-based approach to concurrency allows particularly elegant
solutions to some problems, as in the prime sieve example, adapted
from \cite{NALfCwM}, shown below. In this example each time a new prime
number is found a process is spawned to filter out multiples of that
prime number. These processes are strung together like beads on a string.

\begin{lstlisting}

#!/usr/bin/env oh
define strict True

Root::public read-car: method self () as: car: self::read

define counter: method (n) as {
    while True {
        write: set n: add n 1
    }
}

define filter: method (base) as {
    while True {
	define n: read-car
        if (mod n base): write n
    }
}

define prime-numbers: channel

counter 2 |+ block {
    define in $stdin

    while True {
        define prime: in::read-car
        write prime

        define out: channel
        spawn: filter prime <in >out

        set in out
    }
} > prime-numbers &

define count: integer 1000
printf "The first %d prime numbers" count

define line ""
while count {
    define p: read-car
    set line: Text::join "" line: Text::sprintf "%7d" p
    set count: sub count 1
    if (not: mod count 10) {
        echo line
	set line ""
    }
} <prime-numbers
\end{lstlisting}

%Builtins can be created with 'builtin' TODO - globbing, no passing
%of complex types.

\bibliographystyle{abbrv}
\bibliography{oh}

\end{document}

